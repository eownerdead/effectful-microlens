module Effectful.State.VARIANT.Microlens
(
  -- * Getting
  use, preuse,

  -- * Setting
  (%=), modifying,
  (.=), assign,
  (?=),
  (<~),

  -- * Convenience
  (&~),

  -- * Specialised modifying operators
  -- $arith-note
  (+=), (-=), (*=), (//=),

  -- * Setting with passthrough
  (<%=), (<.=), (<?=),
  (<<%=), (<<.=),
)where


import Data.Monoid

import Effectful
import Effectful.State.VARIANT

import Lens.Micro
import Lens.Micro.Extras


{- |
'use' is ('^.') (or 'view') which implicitly operates on the state; for instance, if your state is a record containing a field @foo@, you can write

@
x \<- 'use' foo
@

to extract @foo@ from the state. In other words, 'use' is the same as 'State.gets', but for getters instead of functions.

The implementation of 'use' is straightforward:

@
'use' l = 'State.gets' ('view' l)
@

If you need to extract something with a fold or traversal, you need 'preuse'.
-}
use :: State s :> es => Getting a s a -> Eff es a
use l = gets (view l)
{-# INLINE use #-}

{- |
'preuse' is ('^?') (or 'preview') which implicitly operates on the state – it takes the state and applies a traversal (or fold) to it to extract the 1st element the traversal points at.

@
'preuse' l = 'State.gets' ('preview' l)
@
-}
preuse :: State s :> es => Getting (First a) s a -> Eff es (Maybe a)
preuse l = gets (preview l)
{-# INLINE preuse #-}

{- |
This can be used to chain lens operations using @op=@ syntax
rather than @op~@ syntax for simple non-type-changing cases.
>>> (10,20) & _1 .~ 30 & _2 .~ 40
(30,40)

>>> (10,20) &~ do _1 .= 30; _2 .= 40
(30,40)

This does not support type-changing assignment, /e.g./

>>> (10,20) & _1 .~ "hello"
("hello",20)
-}
(&~) :: s -> Eff '[State s] a -> s
s &~ l = runPureEff $ execState s l
{-# INLINE (&~) #-}

infixl 1 &~

{- |
Modify state by “assigning” a value to a part of the state.

This is merely ('.~') which works in 'MonadState':

@
l '.=' x = 'State.modify' (l '.~' x)
@

If you also want to know the value that was replaced by ('.='), use ('<<.=').
-}
(.=) :: State s :> es => ASetter s s a b -> b -> Eff es ()
l .= x = modify (l .~ x)
{-# INLINE (.=) #-}

infix 4 .=

{- |
A synonym for ('.=').
-}
assign :: State s :> es => ASetter s s a b -> b -> Eff es ()
assign l x = l .= x
{-# INLINE assign #-}

{- |
('?=') is a version of ('.=') that wraps the value into 'Just' before setting.

@
l '?=' b = l '.=' Just b
@

It can be useful in combination with 'at'.
-}
(?=) :: State s :> es => ASetter s s a (Maybe b) -> b -> Eff es ()
l ?= b = l .= Just b
{-# INLINE (?=) #-}

infix 4 ?=

{- |
('<~') is a version of ('.=') that takes a monadic value (and then executes it and assigns the result to the lens).

@
l '<~' mb = do
  b <- mb
  l '.=' b
@
-}
(<~) :: State s :> es => ASetter s s a b -> Eff es b -> Eff es ()
l <~ mb = mb >>= (l .=)
{-# INLINE (<~) #-}

infixr 2 <~

{- |
Modify state by applying a function to a part of the state. An example:

>>> execState (do _1 %= (+1); _2 %= reverse) (1,"hello")
(2,"olleh")

Implementation:

@
l '%=' f = 'State.modify' (l '%~' f)
@

If you also want to get the value before\/after the modification, use ('<<%=')\/('<%=').

There are a few specialised versions of ('%=') which mimic C operators:

* ('+=') for addition
* ('-=') for substraction
* ('*=') for multiplication
* ('//=') for division
-}
(%=) :: State s :> es => ASetter s s a b -> (a -> b) -> Eff es ()
l %= f = modify (l %~ f)
{-# INLINE (%=) #-}

infix 4 %=

{- |
A synonym for ('%=').
-}
modifying :: State s :> es => ASetter s s a b -> (a -> b) -> Eff es ()
modifying l f = l %= f
{-# INLINE modifying #-}

{- $arith-note

The following operators mimic well-known C operators ('+=', '-=', etc). ('//=') stands for division.

They're implemented like this:

@
l '+=' x = l '%=' (+x)
l '-=' x = l '%=' ('subtract' x)
...
@
-}

(+=) :: (State s :> es, Num a) => ASetter s s a a -> a -> Eff es ()
l += x = l %= (+x)
{-# INLINE (+=) #-}

infix 4 +=

(-=) :: (State s :> es, Num a) => ASetter s s a a -> a -> Eff es ()
l -= x = l %= (subtract x)
{-# INLINE (-=) #-}

infix 4 -=

(*=) :: (State s :> es, Num a) => ASetter s s a a -> a -> Eff es ()
l *= x = l %= (*x)
{-# INLINE (*=) #-}

infix 4 *=

(//=) :: (State s :> es, Fractional a) => ASetter s s a a -> a -> Eff es ()
l //= x = l %= (/x)
{-# INLINE (//=) #-}

infix 4 //=

{- |
Modify state and return the modified (new) value.

@
l '<%=' f = do
  l '%=' f
  'use' l
@
-}
(<%=) :: State s :> es => LensLike ((,) b) s s a b -> (a -> b) -> Eff es b
l <%= f = l %%= (\a -> (a, a)) . f
{-# INLINE (<%=) #-}

infix 4 <%=

{- |
Modify state and return the old value (i.e. as it was before the modificaton).

@
l '<<%=' f = do
  old <- 'use' l
  l '%=' f
  return old
@
-}
(<<%=) :: State s :> es => LensLike ((,) a) s s a b -> (a -> b) -> Eff es a
l <<%= f = l %%= (\a -> (a, f a))
{-# INLINE (<<%=) #-}

infix 4 <<%=

{- |
Set state and return the old value.

@
l '<<.=' b = do
  old <- 'use' l
  l '.=' b
  return old
@
-}
(<<.=) :: State s :> es => LensLike ((,) a) s s a b -> b -> Eff es a
l <<.= b = l %%= (\a -> (a, b))
{-# INLINE (<<.=) #-}

infix 4 <<.=

{- |
Set state and return new value.

@
l '<.=' b = do
  l '.=' b
  return b
@
-}
(<.=) :: State s :> es => LensLike ((,) b) s s a b -> b -> Eff es b
l <.= b = l <%= const b
{-# INLINE (<.=) #-}

infix 4 <.=

{- |
('<?=') is a version of ('<.=') that wraps the value into 'Just' before setting.

@
l '<?=' b = do
  l '.=' Just b
  'return' b
@

It can be useful in combination with 'at'.
-}
(<?=) :: State s :> es => LensLike ((,) b) s s a (Maybe b) -> b -> Eff es b
l <?= b = l %%= const (b, Just b)
{-# INLINE (<?=) #-}

infix 4 <?=

(%%=) :: State s :> es => LensLike ((,) r) s s a b -> (a -> (r, b)) -> Eff es r
l %%= f = state (l f)
{-# INLINE (%%=) #-}

infix 4 %%=
